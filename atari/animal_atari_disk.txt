10 POKE 82,0: PRINT CHR$(125): POKE 85,16: PRINT "ANIMAL"
20 POKE 85,3: PRINT "CREATIVE COMPUTING  MORRISTOWN, NJ"
30 PRINT: PRINT: PRINT
40 PRINT "PLAY 'GUESS THE ANIMAL'"
45 PRINT
50 PRINT "THINK OF AN ANIMAL AND THE COMPUTER"
55 PRINT "WILL TRY TO GUESS IT."
60 PRINT
70 W=40: M=100: DIM A$(W*M), L(M), Q$(W), T$(W), V$(W), X$(W), B$(1), C$(W)
71 A$(1)=" ": A$(W*M)=" ":A$(2)=A$(1)
75 FOR I = 0 TO M: L(I)=0: NEXT I
76 GOTO 2000
80 FOR I=0 TO 3
90 READ T$: L(I)=LEN(T$): A$(I*W+1,I*W+L(I))=T$
100 NEXT I
110 N=VAL(A$(0+1)) : REM NOT SURE THIS IS EVER USED
120 REM          MAIN CONTROL SECTION
130 PRINT "ARE YOU THINKING OF AN ANIMAL";:INPUT T$
140 IF T$(1,1)="L" THEN 600
145 IF T$(1,1)="S" THEN 1000
150 IF T$(1,1)<>"Y" THEN 120
160 K=1
170 GOSUB 390
180 IF L(K)=0 THEN 999: REM IF LEN(A$(K))=0 THEN 999
190 IF A$(K*W+1,K*W+2)="\Q" THEN 170 : REM LEFT$(A$(K),2)
200 PRINT "IS IT A ";A$(K*W+3,K*W+L(K)); : REM RIGHT$(A$(K),LEN(A$(K))-2)
210 INPUT T$
220 T$=T$(1,1)
230 IF T$="Y" THEN PRINT "WHY NOT TRY ANOTHER ANIMAL?": GOTO 120
240 PRINT "THE ANIMAL YOU WERE THINKING OF WAS A...";:INPUT V$
250 PRINT "PLEASE TYPE IN A QUESTION THAT WOULD" 
260 PRINT "DISTINGUISH A ";V$:PRINT "FROM A ";A$(K*W+3,K*W+L(K)) : REM RIGHT$(A$(K),LEN(A$(K))-2)
270 INPUT X$
280 PRINT "FOR A ";V$:PRINT "THE ANSWER WOULD BE (Y/N)";
290 INPUT T$
300 T$=T$(1,1): IF T$<>"Y" AND T$<>"N" THEN 280
310 IF T$="Y" THEN B$="N"
320 IF T$="N" THEN B$="Y"
330 Z1=VAL(A$(1,L(0)))
340 C$=STR$(Z1+2): L(0)=LEN(C$): A$(1,L(0))=C$
350 A$(Z1*W+1,Z1*W+L(K))=A$(K*W+1,K*W+L(K)) : L(Z1)=L(K)
360 A$((Z1+1)*W+1)="\A":A$((Z1+1)*W+3)=V$: L(Z1+1)=2+LEN(V$)
370 A$(K*W+1,K*W+2)="\Q"
371 L(K)=2+LEN(X$): A$(K*W+3,K*W+L(K))=X$
372 L(K)=L(K)+1: A$(K*W+L(K),K*W+L(K))="\"
373 L(K)=L(K)+1: A$(K*W+L(K),K*W+L(K))=T$
374 C$=STR$(Z1+1): A$(K*W+L(K)+1,K*W+L(K)+LEN(C$))=C$:L(K)=L(K)+LEN(C$)
375 L(K)=L(K)+1: A$(K*W+L(K),K*W+L(K))="\" 
376 L(K)=L(K)+1: A$(K*W+L(K),K*W+L(K))=B$
377 C$=STR$(Z1): A$(K*W+L(K)+1,K*W+L(K)+LEN(C$))=C$:L(K)=L(K)+LEN(C$)
378 L(K)=L(K)+1: A$(K*W+L(K),K*W+L(K))="\"
380 GOTO 120
390 REM     SUBROUTINE TO PRINT QUESTIONS
400 Q$=A$(K*W+1, K*W+L(K))
410 FOR Z=3 TO LEN(Q$)
415 IF Q$(Z,Z)<>"\" THEN PRINT Q$(Z,Z);: NEXT Z
420 INPUT C$
430 C$=C$(1,1)
440 IF C$<>"Y" AND C$<>"N" THEN 410
450 T$="\": T$(2)=C$
455 FOR X=3 TO LEN(Q$)-1
460 IF Q$(X,X+1)=T$ THEN 480
470 NEXT X
475 STOP
480 FOR Y=X+1 TO LEN(Q$)
490 IF Q$(Y,Y)="\" THEN 510
500 NEXT Y
505 STOP
510 K=VAL(Q$(X+2,Y-1))
520 RETURN
530 DATA 4,\QDOES IT SWIM\Y2\N3\,\AFISH,\ABIRD
600 PRINT:PRINT "ANIMALS I ALREADY KNOW ARE:"
605 REM X=0
610 FOR I=1 TO M-1
615 IF L(I)=0 THEN 650
620 IF A$(I*W+1,I*W+2)<>"\A" THEN 650
REM 624 POKE 85,5*X: REM PRINT TAB(15*X);
630 REM FOR Z=3 TO L(I)
640 PRINT A$(I*W+3,I*W+L(I)), : REM IF A$(W*I+Z,W*I+Z)<>"\" THEN PRINT A$(W*I+Z,W*I+Z);: NEXT Z
645 REM X=X+1: IF X=4 THEN X=0: PRINT
650 NEXT I
660 PRINT
670 PRINT
680 GOTO 120
999 END
1000 REM SAVE DATA
1010 REM OPEN FILE FOR WRITING.
1020 REM FORMAT IS:
1030 REM NUMBER OF STRINGS (FIRST VALUE IN STRING ARRAY)
1040 REM LIST OF ALTERNATING LENGTH AND STRING (EOL SEPERATED) 
1100 OPEN #1, 8, 0, "D:ANIMAL.DAT"
1110 N=VAL(A$(1,L(1)))
1120 PRINT #1;N
1130 FOR I=0 TO N-1
1140 PRINT #1;L(I)
1150 PRINT #1;A$(I*W+1,I*W+L(I))
1160 NEXT I
1170 CLOSE #1
1200 GOTO 120
2000 REM READ DATA
2010 REM OPEN FILE FOR READING.
2020 REM IF ERROR THEN RESORT TO READING THE DATA STATEMENT
2030 REM FORMAT IS SAME AS WRITING
2040 TRAP 2500: OPEN #1,4,0,"D:ANIMAL.DAT"
2050 INPUT #1;N 
2060 FOR I=0 TO N-1
2070 INPUT #1;Z 
2080 INPUT #1;T$
2090 L(I)=Z
2100 A$(I*W+1,I*W+Z)=T$
2110 NEXT I
2120 CLOSE #1
2200 GOTO 120
2500 CLOSE #1
2510 GOTO 80
