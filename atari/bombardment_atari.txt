5 DIM A$(1)
10 POKE 82,0: PRINT CHR$(125): POKE 85,14:PRINT "BOMBARDMENT"
20 POKE 85,4: PRINT "CREATIVE COMPUTING MORRISTOWN, NJ"
30 PRINT:PRINT
100 PRINT "YOU ARE ON A BATTLEFIELD WITH 4 PLATOONS";
110 PRINT "AND YOU HAVE 25 OUTPOSTS AVAILABLE WHERE";
120 PRINT "THEY MAY BE PLACED. YOU CAN ONLY PLACE"
125 PRINT "ONE PLATOON AT ANY ONE OUTPOST. THE"
130 PRINT "COMPUTER DOES THE SAME WITH ITS FOUR":PRINT "PLATOONS."
135 PRINT
140 PRINT "THE OBJECT OF THE GAME IS TO FIRE"
150 PRINT "MISSLES AT THE OUTPOSTS OF THE COMPUTER.";
160 PRINT "IT WILL DO THE SAME TO YOU. THE ONE WHO"
170 PRINT "DESTROYS ALL FOUR ENEMY PLATOONS FIRST":PRINT "IS THE WINNER."
180 PRINT
190 PRINT "GOOD LUCK... AND TELL US WHERE":PRINT "YOU WANT THE BODIES SENT!"
200 PRINT: PRINT "PRESS RETURN FOR NEXT PAGE":INPUT A$
205 PRINT CHR$(125)
210 PRINT "TEAR OFF MATRIX AND USE IT":PRINT "TO CHECK OFF THE NUMBERS."
220 FOR R=1 TO 5: PRINT: NEXT R
260 DIM M(100)
270 FOR R=1 TO 5
280 I=(R-1)*5+1
290 FOR CC = I TO I+4
292 IF CC<10 THEN ?" ";
293 PRINT CC;"   ";
294 NEXT CC 
295 PRINT
300 NEXT R
350 FOR R=1 TO 10: PRINT: NEXT R
380 C=INT(RND(1)*25)+1
390 D=INT(RND(1)*25)+1
400 E=INT(RND(1)*25)+1
410 F=INT(RND(1)*25)+1
420 IF C=D THEN 390
430 IF C=E THEN 400
440 IF C=F THEN 410
450 IF D=E THEN 400
460 IF D=F THEN 410
470 IF E=F THEN 410
480 PRINT "WHAT ARE YOUR FOUR POSITIONS";
490 INPUT G,H,K,L
495 PRINT
500 PRINT "WHERE DO YOU WISH TO FIRE YOUR MISSLE";
510 INPUT Y
520 IF Y=C THEN 710
530 IF Y=D THEN 710
540 IF Y=E THEN 710
550 IF Y=F THEN 710
560 GOTO 630
570 M=INT(RND(1)*25)+1
575 GOTO 1160
580 IF X=G THEN 920
590 IF X=H THEN 920
600 IF X=L THEN 920
610 IF X=K THEN 920
620 GOTO 670
630 PRINT "HA, HA YOU MISSED. MY TURN NOW:"
640 PRINT: PRINT: GOTO 570
670 PRINT "I MISSED YOU, YOU DIRTY RAT.":PRINT "I PICKED ";M;". YOUR TURN:"
680 PRINT: PRINT: GOTO 500
710 Q=Q+1
720 IF Q=4 THEN 890
730 PRINT "YOU GOT ONE OF MY OUTPOSTS!"
740 IF Q=1 THEN 770
750 IF Q=2 THEN 810
760 IF Q=3 THEN 850
770 PRINT "ONE DOWN, THREE TO GO."
780 PRINT: PRINT: GOTO 570
810 PRINT "TWO DOWN, TWO TO GO."
820 PRINT: PRINT: GOTO 570
850 PRINT "THREE DOWN, ONE TO GO."
860 PRINT: PRINT: GOTO 570
890 PRINT "YOU GOT ME, I'M GOING FAST. BUT I'LL"
900 PRINT "GET YOU WHEN MY TRANSISTO&S RECUP%RA*E!"
910 GOTO 1235
920 Z=Z+1
930 IF Z=4 THEN 1110
940 PRINT "I GOT YOU. IT WON'T BE LONG NOW.":PRINT " POST ";X;" WAS HIT."
950 IF Z=1 THEN 990
960 IF Z=2 THEN 1030
970 IF Z=3 THEN 1070
990 PRINT "YOU HAVE ONLY THREE OUTPOSTS LEFT."
1000 PRINT: PRINT: GOTO 500
1030 PRINT "YOU HAVE ONLY TWO OUTPOSTS LEFT."
1040 PRINT: PRINT: GOTO 500
1070 PRINT "YOU HAVE ONLY ONE OUTPOST LEFT."
1080 PRINT: PRINT: GOTO 500
1110 PRINT "YOU'RE DEAD.":PRINT " YOUR LAST OUTPOST WAS AT ";X;".":PRINT " HA, HA, HA."
1120 PRINT "BETTER LUCK NEXT TIME."
1150 GOTO 1235
1160 P=P+1
1170 N=P-1
1180 FOR T=1 TO N
1190 IF M=M(T) THEN 570
1200 NEXT T
1210 X=M
1220 M(P)=M
1230 GOTO 580
1235 END
