10 POKE 82,0: PRINT CHR$(125): POKE 85,17:PRINT "BOUNCE"
20 POKE 85,4: PRINT "CREATIVE COMPUTING MORRISTOWN, NJ"
30 PRINT:PRINT
90 DIM T(20)
100 PRINT "THIS SIMULATION LETS YOU SPECIFY THE":PRINT "INITIAL VELOCITY OF A BALL THROWN"
120 PRINT "STRAIGHT UP, AND THE COEFFICIENT OF": PRINT" ELASTICITY OF THE BALL.  PLEASE USE "
130 PRINT "A DECIMAL FRACTION COEFFICIENT (LESS": PRINT"THAN 1)."
131 PRINT
132 PRINT "YOU ALSO SPECIFY THE TIME INCREMENT TO"
133 PRINT "BE USED IN 'STROBING' THE BALL'S FLIGHT":PRINT "(TRY .1 INITIALLY)."
134 PRINT
135 PRINT "TIME INCREMENT (SEC)";:INPUT S2
140 PRINT
150 PRINT "VELOCITY (FPS)";:INPUT V
160 PRINT
170 PRINT "COEFFICIENT";:INPUT C
180 GRAPHICS 7
182 COLOR 1
184 PRINT
186 S1=INT(70/(V/(16*S2)))
190 FOR I=1 TO S1
200 T(I)=V*C^(I-1)/16
210 NEXT I
220 FOR H=INT(-16*(V/32)^2+V^2/32+.5) TO 0 STEP -.5
221 IF INT(H)<>H THEN 225
222 PRINT H;
225 L=0
230 FOR I=1 TO S1
240 FOR T=0 TO T(I) STEP S2
245 L=L+S2
250 IF ABS(H-(.5*(-32)*T^2+V*C^(I-1)*T))>.25 THEN 270
260 PLOT (L/S2),ROW
270 NEXT T
275 T=T(I+1)/2:IF T<1E-10 THEN T=0
276 IF -16*T^2+V*C^(I-1)*T<H THEN 290
280 NEXT I
290 ROW=ROW+1
300 NEXT H
310 POKE 85,(1)
320 FOR I=1 TO INT(L+1)/S2+1
330 PRINT ".";
340 NEXT I
350 PRINT
355 PRINT " 0";
360 FOR I=1 TO INT(L+.9995)
380 POKE 85,(INT(I/S2)):PRINT I;
390 NEXT I
400 PRINT
410 POKE 85,(INT(L+1)/(2*S2)-2):PRINT "SECONDS"
420 PRINT
430 GOTO 135
440 END

